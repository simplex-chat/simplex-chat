buildscript {
    Properties localProperties = new Properties()
    if (rootProject.file('local.properties').canRead()) {
        localProperties.load(rootProject.file("local.properties").newDataInputStream())
    }

    ext {
        compose_version = localProperties['compose_version'] ?: '1.2.0-beta02'
        kotlin_version = localProperties['kotlin_version'] ?: '1.6.21'
        gradle_plugin_version = localProperties['gradle_plugin_version'] ?: '7.2.0'

        // Whether the app is debuggable or not. Specify `false` if you want good performance in debug builds
        enable_debuggable = localProperties['debuggable'] ?: true
        // Ending part of package name. Allows debug & release versions to coexist
        application_id_suffix = localProperties['application_id_suffix'] ?: '.debug'
        // Provide `application_id_suffix=` in local.properties file if you have old installation
        // and want to preserve your data
        if (localProperties.getProperty('application_id_suffix') == '') {
            application_id_suffix = ''
        }

        // NOTE: If you need a different version of something, provide it in `local.properties`
        // like so: compose_version=123, or gradle_plugin_version=1.2.3, etc
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.3.2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version "$gradle_plugin_version" apply false
    id 'com.android.library' version "$gradle_plugin_version" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}